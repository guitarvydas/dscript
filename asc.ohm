asc {
ASCs = ASC+

ASC = ASCname ASCbody

ASCname = ASCdefName InputPortDef* Connector OutputPortDef*

ASCbody
  = Leaf
  | Container

Leaf = CodeBlock
Container = Children Connections

InputPortDef = MaybePort
OutputPortDef = MaybePort
	  
MaybePort
  = "(" ")" -- empty
  | Port    -- port
  
CodeBlock = "code" codeBlock
Children = "children" Child+
Connections = "connections" Connection+

Connection
  = ConnectionSelfSender
  | ConnectionChildSender
ConnectionChildSender = ChildSender Connector Receiver MoreReceiver*
ConnectionSelfSender = SelfSender Connector Receiver MoreReceiver*

MoreReceiver = "," Receiver

SelfSender = "[" "$self" "]" SenderPort
ChildSender = SenderName SenderPort

Receiver 
  = "$return"                 -- return
  | ReceiverName ReceiverPort -- child

Child = Component
ASCdefName = Component



SenderName = Component
SenderPort = Port
ReceiverName = Component
ReceiverPort = Port

Component = "[" componentName "]"
componentName
  = "$self"            -- self
  | componentNameChar+ -- chars
componentNameChar
  = ~"$self" ~"]" any
Port = "(" portName ")"
portName
  = "$in"         -- in
  | "$out"        -- out
  | portNameChar+ -- chars
portNameChar
  = ~"$in" ~"$out" ~")" any
Connector = ">>"
codeBlock = "@{" language anyChar+ "}"
language = "bash"
anyChar
  = "{" anyChar* "}" -- nested
  | ~"}" any         -- flat
}
